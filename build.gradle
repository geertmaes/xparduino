plugins {
    id 'io.spring.dependency-management' version '1.0.2.RELEASE' apply false
}

project.ext {
    javaProjects = subprojects.findAll {
        it.name == 'xparduino-library' ||
        it.name == 'xparduino-rest-api'
    }
}

subprojects {
    apply plugin: 'idea'

    group applicationGroup
    version applicationVersion
}

configure(project.javaProjects) {
    apply plugin: 'java'
    apply plugin: 'maven-publish'
    apply plugin: 'io.spring.dependency-management'

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    repositories {
        mavenLocal()
        mavenCentral()
    }

    ext {
        jccVersion = '2.8.0'
        slf4jVersion = '1.7.5'
        junitVersion = '4.12'
        assertJVersion = '3.8.0'
        mockitoVersion = '2.8.9'
        hamcrestVersion = '1.3'
        logbackVersion = '1.2.3'
        springBootVersion = '1.5.3.RELEASE'
    }

    dependencyManagement {
        dependencies {
            dependency "org.scream3r:jssc:$jccVersion"
            dependency "org.slf4j:slf4j-api:$slf4jVersion"
            dependency "ch.qos.logback:logback-core:$logbackVersion"
            dependency "ch.qos.logback:logback-classic:$logbackVersion"
            dependency "junit:junit:$junitVersion"
            dependency "org.assertj:assertj-core:$assertJVersion"
            dependency "org.mockito:mockito-core:$mockitoVersion"
            dependency "org.hamcrest:hamcrest-all:$hamcrestVersion"
            dependency "org.springframework.boot:spring-boot-starter-web:$springBootVersion"
            dependency "org.springframework.boot:spring-boot-starter-test':$springBootVersion"
        }
    }

    publishing {
        publications {
            mavenJava(MavenPublication) {
                from components.java
            }
        }
        repositories {
            mavenLocal()
        }
    }
}